#version 450

//常量
float PI = 3.1415926535f;

//struct
//现在就一个pos，但是以后需要采样texture时或者别的时候可以再加
struct Vertex {
	vec4 pos;
	vec4 normal;
};

struct AABBBox {
	float leftX;
	float rightX;
	float leftY;
	float rightY;
	float leftZ;
	float rightZ;
};

struct BvhArrayNode {
	int leftNodeIndex;
	int rightNodeIndex;
	AABBBox AABB;
	int meshIndex;
};

struct Material {
	//bxdfPara.x表示roughness，y表示metallic，z表示refractivity，若z = 1表示不考虑折射
	vec4 bxdfPara;
	vec4 kd;
	vec4 ks;
	vec4 ke;
};

struct Mesh {
	Material material;
	ivec2 indexInIndicesArray;
	AABBBox AABB;
};

struct Ray{
	vec3 startPos;
	vec3 direction;
	vec3 normal;	//这个主要是记录上一个着色点的法线
	//vec3 radiance;
	float depth;
};

layout(set = 0, binding = 0) uniform LightUniformBufferObject {
	mat4 model;
	mat4 view;
	mat4 proj;
	vec4 lightPos_strength;
	vec4 normal;
	vec4 size;
} lubo;

layout(set = 1, binding = 0) uniform cameraUniformBufferObject {
	mat4 model;
	mat4 view;
	mat4 proj;
	vec4 cameraPos;
	vec4 randomNumber;
} cubo;
layout(set = 2, binding = 0, std430) readonly buffer BvhArray{
	BvhArrayNode bvhArrayNode[];
};
layout(set = 2, binding = 1, std430) readonly buffer Vertices{
	Vertex vertices[]; 
};
layout(set = 2, binding = 2, std430) readonly buffer Indices{
	uint indices[]; 
};
layout(set = 2, binding = 3, std430) readonly buffer Meshs{
	Mesh meshs[]; 
};

layout(set = 2, binding = 4) uniform sampler2D shadowMap;
layout(set = 2, binding = 5, rgba8) uniform writeonly image2D pathTracingResult;

layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

//function

//----------------------------createRay-------------------------------------------
//创造射线
Ray makeStartRay(vec2 texelUV){

	vec4 ndcPos = vec4(texelUV * 2.0f - 1.0f, 0.0f, 1.0f);
	vec4 worldPos = inverse(cubo.proj * cubo.view) * ndcPos;
	Ray ray;
	ray.startPos = cubo.cameraPos.xyz;
	ray.direction = normalize((worldPos.xyz / worldPos.w) - ray.startPos);	//得到的worldPos是近平面的一点
	ray.normal = ray.direction;//normalize(transpose(mat3(cubo.view)) * vec3(0.0f, 0.0f, -1.0f));	//逆矩阵的转置的逆 = 转置 
	ray.depth = 100;	//远平面

	return ray;

}

//----------------------------randomNumber-------------------------------------------

uint pcg(inout uint state)
{
    uint prev = state * 747796405u + 2891336453u;
    uint word = ((prev >> ((prev >> 28u) + 4u)) ^ prev) * 277803737u;
    state = prev;
    return (word >> 22u) ^ word;
}

uvec2 pcg2d(uvec2 v)
{
    v = v * 1664525u + 1013904223u;
    v.x += v.y * 1664525u;
    v.y += v.x * 1664525u;
    v = v ^ (v >> 16u);
    v.x += v.y * 1664525u;
    v.y += v.x * 1664525u;
    v = v ^ (v >> 16u);
    return v;
}

float rand(inout uint seed)
{
    uint val = pcg(seed);
    return (float(val) * (1.0 / float(0xffffffffu)));
}


//低差异序列
float RadicalInverse_VdC(uint bits) 
{
    bits = (bits << 16u) | (bits >> 16u);
    bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
    bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
    bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
    bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
    return float(bits) * 2.3283064365386963e-10; // / 0x100000000
}
vec2 Hammersley(uint i, uint N)
{
    return vec2(float(i)/float(N), RadicalInverse_VdC(i));
}

//----------------------------getDirection-------------------------------------------

//需要材质、TBN
vec4 getRayReflectDirection(Material material, mat3 TBN, vec3 i, vec3 normal, float randomNumber){

	vec2 randomNumberH = Hammersley(uint(randomNumber * 100), 100);

	float roughness =  max(material.bxdfPara.x, 0.1f);	//若粗糙度为0，D将是0/0，且趋近于无穷，除非分子上是a4，但是又会导致不是趋近于0时的值的错误
	float a2 = roughness * roughness * roughness * roughness;
	float phi = 2.0 * PI * randomNumberH.x;
	//若roughness是1，则cosTheta是sqrt(1-randomNumberH.y)，所以完全粗糙的情况下，就是cos加权，且pdf也就是cosTheta / PI
	float cosTheta = sqrt((1.0 - randomNumberH.y) / (1.0 + (a2 - 1.0) * randomNumberH.y));
	float sinTheta = sqrt(1.0 - cosTheta * cosTheta);

	vec3 h = normalize(vec3(sinTheta * cos(phi), sinTheta * sin(phi), cosTheta));
	h = normalize(TBN * h);

	float d = (a2 - 1) * cosTheta * cosTheta + 1;
	float D = a2 / (PI * d * d);
	float pdf = D * cosTheta;	//h的pdf

	vec3 rayDirection = normalize(2.0f * dot(h, i) * h - i);

	if(dot(rayDirection, normal) > 0.0f){
		pdf = pdf / (4 * dot(h, i));	//出射的pdf
	}else{
		rayDirection = h;	//将h当作出射向量
	}

	return vec4(rayDirection, clamp(pdf, 0.01f, 1.0f));


}

//不考虑同时发射折射与反射，并且这里不考虑毛玻璃等粗糙折射的情况，只考虑单根折射光线
vec4 getRayRefractDirection(Material Material, vec3 normal, vec3 i, float startRefactivity, float hitRefactivity){

	float cosTheta1 = dot(normal, -i);
	float sinTheta2 = sqrt(1 - cosTheta1 * cosTheta1) * startRefactivity / hitRefactivity;	//n1 * sinTheta1 = n2 * sinTheta2 斯涅尔定理，其中n为反射率
	float cosTheta2 = sqrt(1 - sinTheta2 * sinTheta2);
	//就是入射向量投影到法线，得到入射光 - 法线的单位向量，然后让这个单位向量乘以在折射向量与法线之间的距离，然后让投影法线与之相加，再取负
	vec3 rayDirection =  cosTheta1 * normal + normalize(-i - cosTheta1 * normal) * (cosTheta1 / cosTheta2 * sinTheta2);
	return vec4(-rayDirection, 1.0f);
	
}


//-----------------------------------hitTest-----------------------------------------------

//AABB碰撞检测，-1表示没有碰撞到，0表示碰撞到了且不是在场景内部，1表示在场景内部
bool hitAABB(AABBBox AABB, Ray ray){

	//这里并不能检测出是否朝向AABB，反而会导致原本该命中的被剔除了，尤其是有些vertex，其发射方向与AABB中心除以一个夹角，导致只有与夹角小于90度的地方才能命中
	//vec3 AABBCenter = vec3(AABB.leftX + (AABB.rightX - AABB.leftX) / 2, 
	//					   AABB.leftY + (AABB.rightY - AABB.leftY) / 2,
	//					   AABB.leftZ + (AABB.rightZ - AABB.leftZ) / 2);
	////判断ray的方向是否朝向AABB，并且不能射向发射点的AABB
	//if(dot(AABBCenter - ray.startPos, ray.direction) <= 0.0f){
	//	return false;
	//}
	
	//判断光线是不是在场景内部发出的，如果是还不能直接抛弃别的场景
	//虽然会导致每次都与自身发射点的AABB再检测一次hitMesh
	if(ray.startPos.x > AABB.leftX && ray.startPos.x < AABB.rightX && 
	   ray.startPos.y > AABB.leftY && ray.startPos.y < AABB.rightY &&
	   ray.startPos.z > AABB.leftZ && ray.startPos.z < AABB.rightZ){
		return true;
	}

	float maxInTime = 0.0f;
	float minOutTime = 1000000.0f;	//超了再说

	if(ray.direction.x != 0){	//直射与面都不考虑
		float leftX = (AABB.leftX - ray.startPos.x) / ray.direction.x;
		float rightX= (AABB.rightX - ray.startPos.x) / ray.direction.x;
		maxInTime = max(min(leftX, rightX), maxInTime);
		minOutTime = min(max(leftX, rightX), minOutTime);
	}

	if(ray.direction.y != 0){
		float leftY = (AABB.leftY - ray.startPos.y) / ray.direction.y;
		float rightY = (AABB.rightY - ray.startPos.y) / ray.direction.y;
		maxInTime = max(min(leftY, rightY), maxInTime);
		minOutTime = min(max(leftY, rightY), minOutTime);
	}

	if(ray.direction.z != 0){
		float leftZ = (AABB.leftZ - ray.startPos.z) / ray.direction.z;
		float rightZ = (AABB.rightZ - ray.startPos.z) / ray.direction.z;
		maxInTime = max(min(leftZ, rightZ), maxInTime);
		minOutTime = min(max(leftZ, rightZ), minOutTime);
	}

	if(minOutTime < maxInTime){
		return false;
	}

	//直接用包围盒中点算可能会导致前面的mesh的AABB的depth反而比后面的大，导致被剔除
	if(maxInTime > ray.depth){
		return false;	//深度测试不通过
	}

	return true;
	

}

//返回碰撞点的mesh索引、三角形面片的第一个indicis索引，没碰撞到则不动
void hitMesh(inout Ray ray, uint meshIndex, inout ivec2 result, float backCull){

	Mesh mesh = meshs[meshIndex];
	//因为我们的叶子场景只有一个mesh，所以能hitMesh检测说明一定能hitAABB
	//if(!hitAABB(mesh.AABB, ray)){
	//	return;
	//}

	uint startVertexIndex = mesh.indexInIndicesArray.x;
	uint endVertexIndex = mesh.indexInIndicesArray.y;
	 
	for(uint i = startVertexIndex; i < endVertexIndex; i += 3){
		vec3 P0 = vertices[indices[i]].pos.xyz;
		vec3 P1 = vertices[indices[i + 1]].pos.xyz;
		vec3 P2 = vertices[indices[i + 2]].pos.xyz;

		vec3 tangent = normalize(P1 - P0);
		vec3 bitangent = normalize(P2 - P0);
		vec3 normal = normalize(cross(tangent, bitangent));
		if(backCull == 1.0f && dot(normal, -ray.direction) < 0){
			continue;	//不折射则背面剔除
		}

		vec3 E1 = P1 - P0;
		vec3 E2 = P2 - P0;
		vec3 S = ray.startPos - P0;
		vec3 S1 = cross(ray.direction, E2);
		vec3 S2 = cross(S, E1);

		vec3 tbb = 1 / dot(S1, E1) * vec3(dot(S2, E2), dot(S1, S), dot(S2, ray.direction));
		if(tbb.x > 0 && (1.0f - tbb.y - tbb.z) > 0 && tbb.y > 0 && tbb.z > 0){	//打到了
			if(tbb.x > ray.depth){
				continue;	//深度测试没通过
			}
			result = ivec2(meshIndex, indices[i]);
			ray.depth = tbb.x;
			return;
		}
	}

}

//由于不能使用递归，我们需要采用栈的方式循环读取
ivec2 hitScene(inout Ray ray, float backCull){
	
	ivec2 result = ivec2(-1, -1);
	//栈的大小需要和和bvh树节点总数相同（最坏情况），应该从CPU中uniform过来的，但是懒得写了，直接用个大小为10的数组，对于我们这个小场景应该够用了
	//第一个表示sceneIndex，第二个是自身是哪个子树，第三个是父结点是否要去除
	ivec3 sceneStack[15] = ivec3[15](ivec3(0, 1, 1), ivec3(-1, -1, -1), ivec3(-1, -1, -1), ivec3(-1, -1, -1), ivec3(-1, -1, -1), ivec3(-1, -1, -1),
									 ivec3(-1, -1, -1), ivec3(-1, -1, -1), ivec3(-1, -1, -1), ivec3(-1, -1, -1), ivec3(-1, -1, -1), ivec3(-1, -1, -1),
									 ivec3(-1, -1, -1), ivec3(-1, -1, -1), ivec3(-1, -1, -1));
	int stackTop = 0;
	while(stackTop >= 0){

		if(sceneStack[stackTop].z == -1){	
			int isRight = sceneStack[stackTop].y;
			sceneStack[stackTop] = ivec3(-1, -1, -1);
			stackTop -= 1;
			if(isRight == 1){
				sceneStack[stackTop].z = -1;
			}
			continue;
		}

		BvhArrayNode scene = bvhArrayNode[sceneStack[stackTop].x];
		if(!hitAABB(scene.AABB, ray)){
			int isRight = sceneStack[stackTop].y;
			sceneStack[stackTop] = ivec3(-1, -1, -1);
			stackTop -= 1;
			if(isRight == 1){
				sceneStack[stackTop].z = -1;
			}
			continue;
		}

		//若是叶子节点，则直接进行mesh碰撞
		if(scene.leftNodeIndex == -1){
			hitMesh(ray, scene.meshIndex, result, backCull);
			int isRight = sceneStack[stackTop].y;
			sceneStack[stackTop] = ivec3(-1, -1, -1);
			stackTop -= 1;
			if(isRight == 1){
				sceneStack[stackTop].z = -1;
			}
			continue;
		}

		//先将左右子树压栈，先遍历左子树再右子树
		stackTop += 1;
		sceneStack[stackTop] = ivec3(scene.rightNodeIndex, 1, 1);
		stackTop += 1;
		sceneStack[stackTop] = ivec3(scene.leftNodeIndex, 0, 1);

	}

	return result;

}

//-------------------------------------------bxdf---------------------------------------------------------

float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a = roughness * roughness;
    float a2 = a * a;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH * NdotH;

    float nom   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return nom / denom;
}

float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r * r) / 8.0;

    float nom   = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}

float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx2 = GeometrySchlickGGX(NdotV, roughness);
    float ggx1 = GeometrySchlickGGX(NdotL, roughness);

    return ggx1 * ggx2;
}

vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

//-------------------------------------------radiance---------------------------------------------------------

//若当前hitPoint是i，那么currentPos是i-1，startPos是i-2，也就是说只有当i >= 2时才计算weight
vec3 getWeight(vec3 startPos, vec3 currentPos, vec3 currentNormal, vec3 hitPos, Material material){

	vec3 albedo = material.kd.rgb;
	float roughness = material.bxdfPara.x;
	float metallic = material.bxdfPara.y;
	float refractivity = material.bxdfPara.z;

	float diff_fr = 1 / (2 * PI);

	vec3 F0 = vec3(0.04); 
	F0 = mix(F0, albedo, metallic);

	vec3 i = normalize(hitPos - currentPos);

	vec3 o = normalize(startPos - currentPos);

	float cosTheta = max(dot(i, currentNormal), 0.0f);

	vec3 h = normalize(i + o);
	float NDF = DistributionGGX(currentNormal, h, roughness);        
	float G = GeometrySmith(currentNormal, o, i, roughness);      
	vec3 F = fresnelSchlick(max(dot(h, o), 0.0), F0); 

	vec3 nominator = NDF * G * F;
	float denominator = 4.0 * max(dot(currentNormal, o), 0.0) * max(dot(currentNormal, i), 0.0) + 0.001; 
	vec3 spec_fr = nominator / denominator;

	vec3 ks = F;
	vec3 kd = vec3(1.0) - ks;
	kd *= 1.0 - metallic;

	return (kd * albedo * diff_fr + ks * spec_fr) * cosTheta;

}

//不应该通过shadowMap获得阴影，应该直接向光源发出射线，通过ray.depth来判断
//但是太卡了，还是用shadowMap吧
float getShadow(vec3 worldPos, vec3 lightPos){

	
	vec4 clipPos = lubo.proj * lubo.view * lubo.model * vec4(worldPos, 1.0f);
	vec3 ndcPos = clipPos.xyz / clipPos.w;
	vec2 uv = (ndcPos.xy * 0.5f + 0.5f);

	float depth = texture(shadowMap, uv).x;
	if(ndcPos.z + 0.001f < depth){
		return 1.0f;
	}
	return 0.0f;
	

	/*
	Ray ray;
	ray.startPos = worldPos;
	ray.direction = normalize(lightPos - worldPos);
	ray.depth = 100.0f;
	
	hitScene(ray);
	return abs(ray.depth - length(lightPos - worldPos)) < 0.05f ? 1.0f : 0.0f;
	*/

}

int lightA = 36;
int sqrtLightA = 6;
//对于光源均匀求值，所以微分单元应该是dA，而pdf为1/A
//这里是在算撞击点接收到的光源的radiance
vec3 getRadianceFromLight(vec3 startPos, vec3 hitPos, vec3 normal, Material material){

	vec3 radiance = vec3(0.0f);

	vec3 lightPos = lubo.lightPos_strength.xyz;
	vec3 lightStrength = vec3(27.0f, 22.0f, 14.0f);
	vec3 lightNormal = normalize(lubo.normal.xyz);
	vec4 lightSize = lubo.size;

	float pdf = 1 / float(lightA);
	vec3 o = normalize(startPos - hitPos);
	vec3 Li = lightStrength / float(lightA);
	float diff_fr = 1 / (2 * PI);

	vec3 albedo = material.kd.rgb;
	float roughness = material.bxdfPara.x;
	float metallic = material.bxdfPara.y;
	float refractivity = material.bxdfPara.z;

	vec3 F0 = vec3(0.04); 
	F0 = mix(F0, albedo, metallic);

	//将面光源分为3x3的小点光源
	for(int i = 1; i < sqrtLightA * 2; i += 2){
		vec3 lightPointPos = lightPos + vec3(lightSize.x, 0.0f, 0.0f) * i / (sqrtLightA * 2);
		for(int j = 1; j < sqrtLightA * 2; j += 2){

			lightPointPos = lightPos + vec3(0.0f, 0.0f, lightSize.z) * j / (sqrtLightA * 2);
			
			vec3 i = normalize(lightPointPos - hitPos);
			vec2 cosTheta = vec2(max(dot(-i, lightNormal), 0.0f), max(dot(i, normal), 0.0f));
			float distance = max(length(lightPointPos - hitPos), 1.0f);

			float other = cosTheta.x * cosTheta.y / (distance * distance);

			vec3 h = normalize(i + o);
			float NDF = DistributionGGX(normal, h, roughness);        
			float G = GeometrySmith(normal, o, i, roughness);      
			vec3 F = fresnelSchlick(max(dot(h, o), 0.0), F0); 

			vec3 nominator = NDF * G * F;
			float denominator = 4.0 * max(dot(normal, o), 0.0) * max(dot(normal, i), 0.0) + 0.001; 
			vec3 spec_fr = nominator / denominator;

			vec3 ks = F;
			vec3 kd = vec3(1.0) - ks;
			kd *= 1.0 - metallic;  

			radiance += (kd * albedo * diff_fr + ks * spec_fr) * Li * other / pdf * getShadow(hitPos, lightPointPos);

		}
	}

	return radiance / float(lightA);

}

//计算撞击点的自发光
vec3 getRadianceFromHitPoint(Material material, vec3 startPos, vec3 hitPos, vec3 hitNormal) {
	
	if(material.ke.rgb != vec3(0.0f)){
		float cosTheta = dot(normalize(hitPos - startPos), hitNormal);
		return material.ke.rgb / float(lightA) *  abs(cosTheta);	//场景就一个光源有自发光，所以写死
	}
	return vec3(0.0f);
}

//---------------------------------------------pathTracing---------------------------------------------

void getHitPointNormalAndTBN(int vertexIndex, inout vec3 normal, vec3 i, inout mat3 TBN){

	//计算面法线
	vec3 P0 = vertices[vertexIndex].pos.xyz;
	vec3 P1 = vertices[vertexIndex + 1].pos.xyz;
	vec3 P2 = vertices[vertexIndex + 2].pos.xyz;
	
	vec3 tangent = normalize(P1 - P0);
	vec3 bitangent = normalize(P2 - P0);
	normal = normalize(cross(tangent, bitangent));
	bitangent = normalize(cross(normal, tangent));
	TBN = mat3(tangent, bitangent, normal);

	//原本采取均匀出射，那么需要将符合均匀分布的出射向量转移到以normal为轴的世界空间
	//但是现在采取GGX重要性采样，符合反射波瓣分布的出射向量要转移到以反射向量为轴的世界空间下，所以现在TBN的N应该是反射方向
	//tangent = normal;
	//vec3 sampleNormal = normalize(reflect(i, normal));	//i已经是指向顶点的了，即ray.direction
	//bitangent = normalize(cross(sampleNormal, tangent));
	//tangent = normalize(cross(bitangent, sampleNormal));
	//TBN = mat3(tangent, bitangent, sampleNormal);
	
}

vec3 getOneSPP(Ray ray, uint randomNumberSeed){

	vec3 radiance = vec3(0.0f);
	mat3 TBN = mat3(1.0f);
	vec3 hitPos = vec3(1.0f);
	Material material;
	float pdf = 1.0f;
	vec3 weight = vec3(1.0f);
	float RR = 0.8f;	//轮盘赌概率
	float normalSign = 1.0f;	//-1说明是内到内，我们不需要剔除背面，而1表示外到内或内到外，需要剔除背面

	ivec2 result = hitScene(ray, normalSign);
	if(result.x == -1){
		return vec3(0.0f);
	}

	int meshIndex = result.x;
	int vertexIndex = result.y;
	hitPos = ray.startPos + ray.depth * ray.direction;

	vec3 normal;
	getHitPointNormalAndTBN(vertexIndex, normal, ray.direction, TBN);	//计算面法线

	material = meshs[meshIndex].material;

	float randomNumber = rand(randomNumberSeed);
	randomNumber = smoothstep(0.0f, 1.0f, randomNumber);
	vec4 directionAndPdf = vec4(1.0f);
	if(material.bxdfPara.z != 1.0f){
		normal *= normalSign;	//外到内，内到外，normal为正，内到内，normal为负
		normalSign *= -1.0f;
		//我们只考虑单折射或单反射，所以若发生折射，则不算光的直接照射
		vec3 hitPointEmission = getRadianceFromHitPoint(material, ray.startPos, hitPos, normal);
		radiance += hitPointEmission;
		directionAndPdf = getRayRefractDirection(material, normal, ray.direction, 1.0f, material.bxdfPara.z);
	}else{
		vec3 lightRadiance = getRadianceFromLight(ray.startPos, hitPos, normal, material);	//发过来的radiance
		vec3 hitPointEmission = getRadianceFromHitPoint(material, ray.startPos, hitPos, normal);
		vec3 irradiance = lightRadiance + hitPointEmission;
		radiance += irradiance;		//weight = 1
		directionAndPdf = getRayReflectDirection(material, TBN, -ray.direction, normal, randomNumber);
	}

	pdf = directionAndPdf.w;

	if(rand(randomNumberSeed) > RR){
		return radiance;
	}

	vec3 lastHitPos = ray.startPos;

	ray.direction = normalize(directionAndPdf.xyz);
	ray.startPos = hitPos + 0.01f * ray.direction;	//加一点bias，其实没啥用，但是心里感觉有用，加上吧，心里踏实
	ray.normal = normal;
	ray.depth = 100.0f;

	Material lastMaterial = material;
	int lossNum = 0;
	int lossNumMax = 5;
	int maxShootNum = 6;
	while(maxShootNum > 0){
		
		lossNum = 0;
		ivec2 result = hitScene(ray, normalSign);
		while(lossNum <= lossNumMax && result.x == -1){

			randomNumberSeed += lossNum * 100;
			float randomNumber = 0.8f * (float(lossNum) / float(lossNumMax)) + 0.2f * rand(randomNumberSeed);
			if(material.bxdfPara.z != 1.0f){
				return radiance;	//折射方向固定，没打中直接返回
			}
			directionAndPdf = getRayReflectDirection(material, TBN, normalize(lastHitPos - ray.startPos), normal, randomNumber);	//每次rand后都会修改randomNumberSeed
			pdf = directionAndPdf.w;

			ray.startPos = ray.startPos - 0.01f * ray.direction + 0.01f * normalize(directionAndPdf.xyz);
			ray.direction = normalize(directionAndPdf.xyz);
			ray.depth = 100.0f;

			lossNum++;
			result = hitScene(ray, normalSign);

		}
		if(lossNum - 1 == lossNumMax){
			return radiance;
		}

		int meshIndex = result.x;
		int vertexIndex = result.y;
		hitPos = ray.startPos + ray.depth * ray.direction;

		vec3 normal;
		getHitPointNormalAndTBN(vertexIndex, normal, ray.direction, TBN);		//计算面法线

		lastMaterial = material;
		material = meshs[meshIndex].material;
		
		randomNumberSeed += maxShootNum * 1000;
		float randomNumber = rand(randomNumberSeed);
		randomNumber = smoothstep(0.0f, 1.0f, randomNumber);
		directionAndPdf = vec4(1.0f);
		if(material.bxdfPara.z != 1.0f){
			vec3 hitPointEmission = getRadianceFromHitPoint(material, ray.startPos, hitPos, normal);
			weight *= material.kd.rgb * abs(dot(normalize(hitPos - ray.startPos), ray.normal)) / pdf / RR;
			radiance += hitPointEmission * weight;
			directionAndPdf = getRayRefractDirection(material, normal * normalSign, ray.direction, lastMaterial.bxdfPara.z, material.bxdfPara.z);
			normalSign *= -1.0f;
		}else{
			normalSign = 1.0f;
			vec3 lightRadiance = getRadianceFromLight(ray.startPos, hitPos, normal, material);	//发过来的radiance
			vec3 hitPointEmission = getRadianceFromHitPoint(material, ray.startPos, hitPos, normal);
			vec3 irradiance = lightRadiance + hitPointEmission;
			weight *= getWeight(lastHitPos, ray.startPos, ray.normal, hitPos, lastMaterial) / pdf / RR;
			radiance += irradiance * weight;
			directionAndPdf = getRayReflectDirection(material, TBN, -ray.direction, normal, randomNumber);
		}

		pdf = directionAndPdf.w;

		if(rand(randomNumberSeed) > RR){
			return radiance;
		}

		lastHitPos = ray.startPos;
		ray.direction = normalize(directionAndPdf.xyz);
		ray.startPos = hitPos + 0.01f * ray.direction;	//加一点bias，其实没啥用，但是心里感觉有用，加上吧，心里踏实
		ray.normal = normal;
		ray.depth = 100.0f;

		maxShootNum--;

	}

	return radiance;

}

void main(){

	vec3 radiance = vec3(0.0f);
	int sppNum = 100;
	vec2 texelSize = 1.0f / (gl_WorkGroupSize * gl_NumWorkGroups).xy;
	for(int i = 0; i < sppNum; i++){
	
		uvec2 seed2 = pcg2d(ivec2(gl_GlobalInvocationID) * (i * 10 + sppNum * uint(floor(cubo.randomNumber.x)) + 1));
		uint seed = seed2.x + seed2.y;
		vec2 texelUV = (vec2(gl_GlobalInvocationID.xy) + vec2(rand(seed), rand(seed))) * texelSize;
		Ray ray = makeStartRay(texelUV);
		radiance += getOneSPP(ray, seed);

	}

	radiance /= sppNum;
	imageStore(pathTracingResult, ivec2(gl_GlobalInvocationID.xy), vec4(radiance, 1.0f));

}


